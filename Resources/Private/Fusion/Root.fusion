prototype(Psmb.Footnote:PregReplaceCallback) {
    @class = 'Psmb\\Footnote\\Fusion\\PregReplaceCallbackImplementation'
}
prototype(Psmb.Footnote:PregMatchAll) {
    @class = 'Psmb\\Footnote\\Fusion\\PregMatchAllImplementation'
}

# Gathers footnotes from text
prototype(Psmb.Footnote:FootnoteProcessor) < prototype(Neos.Fusion:Array) {
    content = Neos.Fusion:Value {
        value = ${value}
        @process.parse = Psmb.Footnote:PregReplaceCallback {
            @if.notInBackend = ${!documentNode.context.inBackend}
            pattern = ${'/<span data-footnote="([^"]+)">([^<]+)<\/span>/'}
            subject = ${value}
            replacementRenderer = ${matches[2] + '<sup class="footnote" id="footnoteSource_' + iterator.cycle + '"><a href="#footnote_' + iterator.cycle + '">' + iterator.cycle + '</a></sup>'}
        }
    }
    footnotes = Neos.Fusion:Collection {
        @if.live = ${node.context.workspaceName == 'live'}
        @process.wrap = ${value && '<ul class="footnoteList">' + value + '</ul>'}
        collection = Psmb.Footnote:PregMatchAll {
            pattern = ${'/<span data-footnote="([^"]+)">([^<]+)<\/span>/'}
            subject = ${value}
        }
        itemName = 'item'
        itemRenderer = Psmb.Footnote:FootnoteProcessor.ItemRenderer
    }
}

prototype(Psmb.Footnote:FootnoteProcessor.ItemRenderer) < prototype(Neos.Fusion:Case) {
    autoLinking {
        condition = ${Configuration.setting('Psmb.Footnote.autoLinking') == true}
        renderer = Psmb.Footnote:FootnoteProcessor.LinkRenderer
    }
    default {
        condition = ${true}
        renderer = Psmb.Footnote:FootnoteProcessor.ListItemRenderer
    }
}

# Detect URI to make it clickable
prototype(Psmb.Footnote:FootnoteProcessor.LinkRenderer) < prototype(Neos.Fusion:Value) {
    element = ${item[1]}
    element.@process {
        linkUrl = Psmb.Footnote:PregReplaceCallback {
            pattern = ${'#(https?:\/\/[^\s]+)#'}
            subject = ${value}
            replacementRenderer = ${'<a href="' + matches[1] + '">' + matches[1] + '</a>'}
        }
        lineBreak = ${Configuration.setting('Psmb.Footnote.nl2br') == true ? String.nl2br(value) : value}
    }
    @context.element = ${this.element}
    value = Psmb.Footnote:FootnoteProcessor.ListItemRenderer
}

prototype(Psmb.Footnote:FootnoteProcessor.ListItemRenderer) < prototype(Neos.Fusion:Component) {
    element = ${String.isBlank(element) == false ? element : item[1]}
    id = ${'footnote_' + iterator.cycle}
    anchor = ${'#footnoteSource_' + iterator.cycle}
    index = ${iterator.cycle}

    renderer = afx`
        <li class="footnoteList-item" id={props.id}>
            <a href={props.anchor}><sup>{props.index}</sup></a>{props.element}
        </li>
        `
}